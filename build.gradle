plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.20'
    id "org.jetbrains.dokka" version "1.4.10.2"
    id "com.jfrog.bintray" version "1.8.5"
    id "maven-publish"
}

group 'net.pogibenko.tlv-parser'
version '1.0.0'

repositories {
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "io.github.microutils:kotlin-logging:$kotlin_logging_version"
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:$log4j_version"
    testImplementation "org.junit.jupiter:junit-jupiter:$junit_version"
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['MyPublication']
    publish = true
    pkg {
        repo = "maven"
        name = "tlv-parser"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    archiveClassifier.set("sources")
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set("javadoc")
    from "$buildDir/dokka/javadoc"
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
//            pom.withXml {
//                def root = asNode()
//                root.appendNode('description', 'Simple lib for tlv parsing')
//                root.appendNode('name', 'tlv-parser')
//                root.appendNode('url', 'https://github.com/dpogibenko/tlv-parser')
//                root.children().last() + pomConfig
//            }
        }
    }
}
